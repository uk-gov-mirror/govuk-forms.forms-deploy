resource "aws_wafv2_ip_set" "system_egress_ips" {
  provider = aws.us-east-1

  name               = "${var.environment_name}-system-egress-ips"
  description        = "Egress IPs for ${var.environment_name} environment"
  scope              = "CLOUDFRONT"
  ip_address_version = "IPV4"

  addresses = [for ip in var.nat_gateway_egress_ips : "${ip}/32"]
}

resource "aws_wafv2_ip_set" "rate_limit_bypass_cidrs" {
  provider = aws.us-east-1

  name               = "${var.environment_name}-rate-limit-bypass-cidrs"
  description        = "List of CIDR blocks we allow to bypass rate limiting rules. This is used to allow the penetration testers to carry out tests that would otherwise get them rate limited."
  scope              = "CLOUDFRONT"
  ip_address_version = "IPV4"

  addresses = var.rate_limit_bypass_cidrs
}

resource "aws_wafv2_ip_set" "ips_to_block" {
  provider = aws.us-east-1

  name               = "${var.environment_name}-ips-to-block"
  description        = "Origin IPs to block for ${var.environment_name} environment"
  scope              = "CLOUDFRONT"
  ip_address_version = "IPV4"

  addresses = var.ips_to_block
}

resource "aws_wafv2_rule_group" "public_form_body_size_limits" {
  provider = aws.us-east-1

  name        = "${var.environment_name}-public-form-body-size-limits"
  description = "Rule group for public form request body size restrictions"
  scope       = "CLOUDFRONT"
  capacity    = 50

  rule {
    # Allow file uploads when filling out a form
    name     = "allow_file_uploads"
    priority = 1

    action {
      allow {}
      # Stop processing
    }

    statement {
      and_statement {
        statement {
          byte_match_statement {
            field_to_match {
              single_header {
                name = "content-type"
              }
            }
            positional_constraint = "STARTS_WITH"
            search_string         = "multipart/form-data"
            text_transformation {
              priority = 1
              type     = "LOWERCASE"
            }
          }
        }
        statement {
          regex_match_statement {
            field_to_match {
              uri_path {}
            }
            # /:mode/:form_id/:form_slug(.locale)/:page_slug(/upload-file)
            regex_string = "^/(?:preview-draft|preview-archived|preview-live|form)/\\d+/[\\w-]+(\\.(cy|en))?/[a-zA-Z\\d]+(?:/upload-file)?$"
            text_transformation {
              priority = 1
              type     = "LOWERCASE"
            }
          }
        }
        statement {
          size_constraint_statement {
            field_to_match {
              body {}
            }
            comparison_operator = "LE"
            size                = var.file_upload_max_size
            text_transformation {
              priority = 1
              type     = "NONE"
            }
          }
        }
      }
    }

    visibility_config {
      cloudwatch_metrics_enabled = true
      metric_name                = "FileUploads"
      sampled_requests_enabled   = false
    }
  }

  rule {
    # Enforce standard maximum size for form response bodies
    # ie. POST requests to standard form fields (text inputs, selections, etc.)
    name     = "allow_standard_form_responses"
    priority = 2

    action {
      allow {}
      # Stop processing
    }

    statement {
      and_statement {
        statement {
          regex_match_statement {
            field_to_match {
              uri_path {}
            }
            # /:mode/:form_id/:form_slug(.locale)/:page_slug(/:answer_index)
            regex_string = "^/(?:preview-draft|preview-archived|preview-live|form)/\\d+/[\\w-]+(\\.(cy|en))?/[a-zA-Z\\d]+(?:/\\d+)?$"
            text_transformation {
              priority = 1
              type     = "LOWERCASE"
            }
          }
        }
        statement {
          size_constraint_statement {
            field_to_match {
              body {}
            }
            comparison_operator = "LE"
            size                = var.standard_form_response_body_max_size
            text_transformation {
              priority = 1
              type     = "NONE"
            }
          }
        }
      }
    }

    visibility_config {
      cloudwatch_metrics_enabled = true
      metric_name                = "StandardFormResponses"
      sampled_requests_enabled   = false
    }
  }

  visibility_config {
    cloudwatch_metrics_enabled = true
    metric_name                = "PublicFormBodySizeLimitsRuleGroup"
    sampled_requests_enabled   = false
  }
}

resource "aws_wafv2_rule_group" "admin_file_upload_body_size_limits" {
  provider = aws.us-east-1

  name        = "${var.environment_name}-admin-file-upload-body-size-limits"
  description = "Rule group for admin file upload request body size restrictions"
  scope       = "CLOUDFRONT"
  capacity    = 50

  rule {
    # Allow file uploads when uploading brand assets
    name     = "allow_brand_asset_uploads"
    priority = 1

    action {
      allow {}
      # Stop processing
    }

    statement {
      and_statement {
        statement {
          byte_match_statement {
            field_to_match {
              single_header {
                name = "content-type"
              }
            }
            positional_constraint = "STARTS_WITH"
            search_string         = "multipart/form-data"
            text_transformation {
              priority = 1
              type     = "LOWERCASE"
            }
          }
        }
        statement {
          regex_match_statement {
            field_to_match {
              uri_path {}
            }
            # POST /brands creates a brand, POST /brands/:id updates one
            regex_string = "^/brands(?:/\\d+)?$"
            text_transformation {
              priority = 1
              type     = "LOWERCASE"
            }
          }
        }
        statement {
          size_constraint_statement {
            field_to_match {
              body {}
            }
            comparison_operator = "LE"
            size                = var.brand_asset_upload_max_size
            text_transformation {
              priority = 1
              type     = "NONE"
            }
          }
        }
      }
    }

    visibility_config {
      cloudwatch_metrics_enabled = true
      metric_name                = "BrandAssetUploads"
      sampled_requests_enabled   = false
    }
  }

  visibility_config {
    cloudwatch_metrics_enabled = true
    metric_name                = "AdminFileUploadBodySizeLimitsRuleGroup"
    sampled_requests_enabled   = false
  }
}

resource "aws_wafv2_regex_pattern_set" "body_size_limit_exempt_paths" {
  provider = aws.us-east-1

  name        = "${var.environment_name}-body-size-limit-exempt-paths"
  description = "Paths that are exempt from the 8 KB request body limit in AWSManagedRulesCommonRuleSet. Request size limits should be enforced at the application layer for these endpoints."
  scope       = "CLOUDFRONT"

  # Bulk options upload when creating or editing a selection question in the admin app
  regular_expression {
    regex_string = "^/forms/\\d+/pages/(?:new|\\d+/edit)/selection/bulk-options$"
  }

  # Guidance for adding/editing a question in the admin app
  regular_expression {
    regex_string = "^/forms/\\d+/pages/(?:new|\\d+/edit)/guidance-preview$"
  }

  # Adding Welsh translations for a form in the admin app
  regular_expression {
    regex_string = "^/forms/\\d+/welsh-translation$"
  }

  # Editing question routes in the admin app
  regular_expression {
    regex_string = "^/forms/\\d+/routes$"
  }
}

resource "aws_wafv2_web_acl" "this" {
  #checkov:skip=CKV_AWS_192:We don't use log4j
  provider = aws.us-east-1

  name        = "cloudfront_waf_${var.environment_name}"
  description = "AWS WAF for the CloudFront Distribution"
  scope       = "CLOUDFRONT"

  default_action {
    allow {}
  }

  visibility_config {
    cloudwatch_metrics_enabled = true
    metric_name                = "OriginIPRateLimit"
    sampled_requests_enabled   = false
  }

  lifecycle {
    create_before_destroy = true
  }

  rule {
    name     = "allow_egress_ips_of_${var.environment_name}_env"
    priority = 10

    action {
      allow {}
      # Stop processing
    }

    statement {
      ip_set_reference_statement {
        arn = aws_wafv2_ip_set.system_egress_ips.arn
      }
    }

    visibility_config {
      cloudwatch_metrics_enabled = true
      metric_name                = "${var.environment_name}_env_system_ips_allowed"
      sampled_requests_enabled   = false
    }
  }

  rule {
    name     = "PublicFormBodySizeLimitsRuleGroup"
    priority = 3

    override_action {
      none {}
    }

    statement {
      rule_group_reference_statement {
        arn = aws_wafv2_rule_group.public_form_body_size_limits.arn
      }
    }

    visibility_config {
      cloudwatch_metrics_enabled = true
      metric_name                = "PublicFormBodySizeLimitsRuleGroup"
      sampled_requests_enabled   = false
    }
  }

  rule {
    name     = "AdminFileUploadBodySizeLimitsRuleGroup"
    priority = 1

    override_action {
      none {}
    }

    statement {
      rule_group_reference_statement {
        arn = aws_wafv2_rule_group.admin_file_upload_body_size_limits.arn
      }
    }

    visibility_config {
      cloudwatch_metrics_enabled = true
      metric_name                = "AdminFileUploadBodySizeLimitsRuleGroup"
      sampled_requests_enabled   = false
    }
  }

  rule {
    name     = "AWSManagedRulesCommonRuleSet"
    priority = 4

    override_action {
      none {}
    }

    statement {
      managed_rule_group_statement {
        name        = "AWSManagedRulesCommonRuleSet"
        vendor_name = "AWS"

        # Counted rather than blocked so that the paths in body_size_limit_exempt_paths
        # can be excluded; the block is applied by BlockOversizeBody below
        rule_action_override {
          name = "SizeRestrictions_BODY"
          action_to_use {
            count {}
          }
        }
      }
    }

    visibility_config {
      cloudwatch_metrics_enabled = true
      metric_name                = "AWSManagedRulesCommonRuleSetMetric"
      sampled_requests_enabled   = true
    }
  }

  rule {
    name     = "AWS-AWSManagedRulesAmazonIpReputationList"
    priority = 5

    override_action {
      none {}
    }

    statement {
      managed_rule_group_statement {
        vendor_name = "AWS"
        name        = "AWSManagedRulesAmazonIpReputationList"

        rule_action_override {
          action_to_use {
            block {}
          }

          name = "AWSManagedIPDDoSList"
        }
      }
    }

    visibility_config {
      cloudwatch_metrics_enabled = true
      metric_name                = "AWS-AWSManagedRulesAmazonIpReputationList"
      sampled_requests_enabled   = true
    }
  }

  rule {
    name     = "AWS-AWSManagedRulesKnownBadInputsRuleSet"
    priority = 6
    override_action {
      none {}
    }
    statement {
      managed_rule_group_statement {
        name        = "AWSManagedRulesKnownBadInputsRuleSet"
        vendor_name = "AWS"
      }
    }
    visibility_config {
      cloudwatch_metrics_enabled = true
      metric_name                = "AWS-AWSManagedRulesKnownBadInputsRuleSet"
      sampled_requests_enabled   = true
    }
  }

  rule {
    name     = "BlockOversizeBody"
    priority = 7

    action {
      block {}
    }

    statement {
      and_statement {
        statement {
          label_match_statement {
            scope = "LABEL"
            key   = "awswaf:managed:aws:core-rule-set:SizeRestrictions_BODY"
          }
        }
        statement {
          not_statement {
            statement {
              regex_pattern_set_reference_statement {
                arn = aws_wafv2_regex_pattern_set.body_size_limit_exempt_paths.arn
                field_to_match {
                  uri_path {}
                }
                text_transformation {
                  priority = 1
                  type     = "LOWERCASE"
                }
              }
            }
          }
        }
      }
    }

    visibility_config {
      cloudwatch_metrics_enabled = true
      metric_name                = "BlockOversizeBody"
      sampled_requests_enabled   = true
    }
  }

  rule {
    name     = "AWS-AWSManagedRulesAntiDDoSRuleSet"
    priority = 15

    override_action {
      none {}
    }

    statement {
      managed_rule_group_statement {
        name        = "AWSManagedRulesAntiDDoSRuleSet"
        vendor_name = "AWS"

        managed_rule_group_configs {
          aws_managed_rules_anti_ddos_rule_set {
            sensitivity_to_block = "LOW"

            client_side_action_config {
              challenge {
                usage_of_action = "ENABLED"
                sensitivity     = "HIGH"

                dynamic "exempt_uri_regular_expression" {
                  for_each = var.anti_ddos_exempt_uri_regular_expressions
                  content {
                    regex_string = exempt_uri_regular_expression.value
                  }
                }
              }
            }
          }
        }
      }
    }

    visibility_config {
      cloudwatch_metrics_enabled = true
      metric_name                = "AWS-AWSManagedRulesAntiDDoSRuleSet"
      sampled_requests_enabled   = true
    }
  }

  rule {
    name     = "OriginIPRateLimit"
    priority = 100

    action {
      block {}
    }

    statement {
      rate_based_statement {
        limit              = var.ip_rate_limit
        aggregate_key_type = "IP"

        scope_down_statement {
          not_statement {
            statement {
              ip_set_reference_statement {
                arn = aws_wafv2_ip_set.rate_limit_bypass_cidrs.arn
              }
            }
          }
        }
      }
    }

    visibility_config {
      cloudwatch_metrics_enabled = true
      metric_name                = "OriginIPRateLimit"
      sampled_requests_enabled   = false
    }
  }

  rule {
    name     = "OriginIPBlock"
    priority = 0

    action {
      block {}
    }

    statement {
      ip_set_reference_statement {
        arn = aws_wafv2_ip_set.ips_to_block.arn
      }
    }

    visibility_config {
      cloudwatch_metrics_enabled = true
      metric_name                = "${var.environment_name}_ips_blocked"
      sampled_requests_enabled   = false
    }

  }
}

resource "aws_cloudwatch_log_group" "waf" {
  #checkov:skip=CKV_AWS_338:We're happy with 30 days retention for now
  #checkov:skip=CKV_AWS_158:Amazon managed SSE is sufficient.
  provider          = aws.us-east-1
  name              = "aws-waf-logs-${var.environment_name}"
  retention_in_days = 30
}

module "cribl_well_known" {
  source = "../well-known/cribl"
}


resource "aws_cloudwatch_log_subscription_filter" "waf" {
  provider = aws.us-east-1

  name = "via-cribl-to-splunk"

  log_group_name = aws_cloudwatch_log_group.waf.name

  filter_pattern  = ""
  destination_arn = module.cribl_well_known.kinesis_destination_arns["us-east-1"]
  distribution    = "ByLogStream"
  role_arn        = var.kinesis_subscription_role_arn
}


resource "aws_wafv2_web_acl_logging_configuration" "this" {
  provider                = aws.us-east-1
  log_destination_configs = [aws_cloudwatch_log_group.waf.arn]
  resource_arn            = aws_wafv2_web_acl.this.arn

  logging_filter {
    default_behavior = "DROP"

    filter {
      behavior    = "KEEP"
      requirement = "MEETS_ANY"

      condition {
        action_condition {
          action = "BLOCK"
        }
      }
      condition {
        action_condition {
          action = "COUNT"
        }
      }
    }
  }
}
